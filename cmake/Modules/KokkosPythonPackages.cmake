#
#   Finds the Packages
#
INCLUDE(KokkosPythonUtilities)

# synchronize Python3_EXECUTABLE and PYTHON_EXECUTABLE
IF(Python3_EXECUTABLE)
    SET(PYTHON_EXECUTABLE ${Python3_EXECUTABLE})
ELSEIF(PYTHON_EXECUTABLE)
    SET(Python3_EXECUTABLE ${PYTHON_EXECUTABLE})
ENDIF()

# basically just used to get Python3_SITEARCH for installation
FIND_PACKAGE(Python3 REQUIRED COMPONENTS Interpreter Development)

FOREACH(_VAR MAJOR MINOR PATCH STRING)
    IF(Python3_VERSION_${_VAR})
        SET(PYTHON_VERSION_${_VAR} ${Python3_VERSION_${_VAR}})
    ENDIF()
ENDFOREACH()

IF(NOT PYTHON_VERSION_STRING)
    IF(PYTHON_VERSION_MAJOR AND PYTHON_VERSION_MINOR AND PYTHON_VERSION_PATCH)
        SET(PYTHON_VERSION_STRING "${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR}.${PYTHON_VERSION_PATCH}" CACHE STRING "Python version" FORCE)
    ELSEIF(PYTHON_VERSION_MAJOR AND PYTHON_VERSION_MINOR)
        SET(PYTHON_VERSION_STRING "${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR}" CACHE STRING "Python version" FORCE)
    ENDIF()
ENDIF()
SET(PYBIND11_PYTHON_VERSION "${PYTHON_VERSION_STRING}" CACHE STRING "Python version" FORCE)
ADD_FEATURE(PYBIND11_PYTHON_VERSION "Python version used by PyBind11")
ADD_FEATURE(PYTHON_VERSION_STRING "Python version found")
ADD_FEATURE(Python3_EXECUTABLE "Python interpreter")

# python binding library
IF(ENABLE_INTERNAL_PYBIND11)
    CHECKOUT_GIT_SUBMODULE(RECURSIVE
    RELATIVE_PATH pybind11
    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR})
    ADD_SUBDIRECTORY(pybind11)
ELSE()
    FIND_PACKAGE(pybind11 REQUIRED)
ENDIF()
